defmodule ReqLLM.Providers.GoogleTest do
  @moduledoc """
  Provider-level tests for Google implementation.

  Tests the provider contract directly without going through Generation layer.
  Focus: prepare_request -> attach -> request -> decode pipeline.
  """

  use ReqLLM.ProviderCase, provider: ReqLLM.Providers.Google

  alias ReqLLM.Context
  alias ReqLLM.Providers.Google

  describe "provider contract" do
    test "provider identity and configuration" do
      assert is_atom(Google.provider_id())
      assert is_binary(Google.base_url())
      assert String.starts_with?(Google.base_url(), "http")
    end

    test "provider schema separation from core options" do
      schema_keys = Google.provider_schema().schema |> Keyword.keys()
      core_keys = ReqLLM.Provider.Options.generation_schema().schema |> Keyword.keys()

      # Provider-specific keys should not overlap with core generation keys
      overlap = MapSet.intersection(MapSet.new(schema_keys), MapSet.new(core_keys))

      assert MapSet.size(overlap) == 0,
             "Schema overlap detected: #{inspect(MapSet.to_list(overlap))}"
    end

    test "provider schema combined with generation schema includes all core keys" do
      full_schema = Google.provider_extended_generation_schema()
      full_keys = Keyword.keys(full_schema.schema)
      core_keys = ReqLLM.Provider.Options.all_generation_keys()

      core_without_meta = Enum.reject(core_keys, &(&1 == :provider_options))
      missing = core_without_meta -- full_keys
      assert missing == [], "Missing core generation keys in extended schema: #{inspect(missing)}"
    end

    test "provider_extended_generation_schema includes both base and provider options" do
      extended_schema = Google.provider_extended_generation_schema()
      extended_keys = extended_schema.schema |> Keyword.keys()

      # Should include all core generation keys
      core_keys = ReqLLM.Provider.Options.all_generation_keys()
      core_without_meta = Enum.reject(core_keys, &(&1 == :provider_options))

      for core_key <- core_without_meta do
        assert core_key in extended_keys,
               "Extended schema missing core key: #{core_key}"
      end

      # Should include provider-specific keys
      provider_keys = Google.provider_schema().schema |> Keyword.keys()

      for provider_key <- provider_keys do
        assert provider_key in extended_keys,
               "Extended schema missing provider key: #{provider_key}"
      end
    end
  end

  describe "stream protocol parsing" do
    test "parses JSON array protocol chunks" do
      first = ~s([{"text":"hello"})
      second = ~s(,{"text":"world"}])

      assert {:incomplete, state} = Google.parse_stream_protocol(first, nil)

      assert {:ok, events, nil} = Google.parse_stream_protocol(second, state)

      assert events == [
               %{data: %{"text" => "hello"}},
               %{data: %{"text" => "world"}}
             ]
    end

    test "waits for complete JSON array when strings contain delimiters" do
      first = ~s([{"text":"look ] here and \\"quote\\"")
      second = ~s(}])

      assert {:incomplete, state} = Google.parse_stream_protocol(first, nil)

      assert {:ok, [%{data: %{"text" => ~s(look ] here and "quote")}}], nil} =
               Google.parse_stream_protocol(second, state)
    end

    test "returns an error for complete malformed JSON array streams" do
      chunk = ~s([{"text":}])

      assert {:error, {:invalid_json_array_stream, %Jason.DecodeError{}}} =
               Google.parse_stream_protocol(chunk, nil)
    end

    test "delegates SSE chunks to SSE parser" do
      chunk = ~s(data: {"text":"hello"}\n\n)

      assert {:ok, [%{data: ~s({"text":"hello"})}], state} =
               Google.parse_stream_protocol(chunk, nil)

      assert %ServerSentEvents.Parser{} = state
    end
  end

  describe "request preparation & pipeline wiring" do
    test "prepare_request creates configured request" do
      {:ok, model} = ReqLLM.model("google:gemini-1.5-flash")
      context = context_fixture()
      opts = [temperature: 0.7, max_tokens: 100]

      {:ok, request} = Google.prepare_request(:chat, model, context, opts)

      assert %Req.Request{} = request
      assert request.url.path == "/models/#{model.model}:generateContent"
      assert request.method == :post
    end

    test "prepare_request for streaming creates streaming endpoint" do
      {:ok, model} = ReqLLM.model("google:gemini-1.5-flash")
      context = context_fixture()
      opts = [temperature: 0.7, max_tokens: 100, stream: true]

      {:ok, request} = Google.prepare_request(:chat, model, context, opts)

      assert %Req.Request{} = request
      assert request.url.path == "/models/#{model.model}:streamGenerateContent"
      assert request.method == :post
    end

    test "attach configures authentication and pipeline" do
      {:ok, model} = ReqLLM.model("google:gemini-1.5-flash")
      opts = [temperature: 0.5, max_tokens: 50]

      request = Req.new() |> Google.attach(model, opts)

      # Verify core options
      assert request.options[:model] == model.model
      assert request.options[:temperature] == 0.5
      assert request.options[:max_tokens] == 50

      # Google uses query parameter authentication
      assert request.options[:params][:key] != nil

      # Verify pipeline steps
      request_steps = Keyword.keys(request.request_steps)
      response_steps = Keyword.keys(request.response_steps)

      assert :llm_encode_body in request_steps
      assert :llm_decode_response in response_steps
    end

    test "error handling for invalid configurations" do
      {:ok, model} = ReqLLM.model("google:gemini-1.5-flash")
      context = context_fixture()

      # Unsupported operation
      {:error, error} = Google.prepare_request(:unsupported, model, context, [])
      assert %ReqLLM.Error.Invalid.Parameter{} = error

      # Provider mismatch
      {:ok, wrong_model} = ReqLLM.model("openai:gpt-4")

      assert_raise ReqLLM.Error.Invalid.Provider, fn ->
        Req.new() |> Google.attach(wrong_model, [])
      end
    end
  end

  describe "body encoding & context translation" do
    test "encode_body without tools" do
      {:ok, model} = ReqLLM.model("google:gemini-1.5-flash")
      context = context_fixture()

      # Create a mock request with the expected structure
      mock_request = %Req.Request{
        options: [
          context: context,
          model: model.model,
          stream: false
        ]
      }

      # Test the encode_body function directly
      updated_request = Google.encode_body(mock_request)

      assert is_binary(updated_request.body)
      decoded = Jason.decode!(updated_request.body)

      # Should have Google's structure
      assert is_list(decoded["contents"])
      # Only user message, system is separate
      assert length(decoded["contents"]) == 1
      refute Map.has_key?(decoded, "tools")

      # Check generationConfig
      assert Map.has_key?(decoded, "generationConfig")
      assert decoded["generationConfig"]["candidateCount"] == 1

      # Check system instruction is separate
      assert Map.has_key?(decoded, "systemInstruction")
      assert decoded["systemInstruction"]["parts"]

      # Only user message in contents now
      [user_msg] = decoded["contents"]
      assert user_msg["role"] == "user"
      assert is_list(user_msg["parts"])
    end

    test "encode_body with tools but no tool_choice" do
      {:ok, model} = ReqLLM.model("google:gemini-1.5-flash")
      context = context_fixture()

      tool =
        ReqLLM.Tool.new!(
          name: "test_tool",
          description: "A test tool",
          parameter_schema: [
            name: [type: :string, required: true, doc: "A name parameter"]
          ],
          callback: fn _ -> {:ok, "result"} end
        )

      mock_request = %Req.Request{
        options: [
          context: context,
          model: model.model,
          stream: false,
          tools: [tool],
          operation: :chat
        ]
      }

      updated_request = Google.encode_body(mock_request)
      decoded = Jason.decode!(updated_request.body)

      assert is_list(decoded["tools"])
      assert length(decoded["tools"]) == 1

      [tool_def] = decoded["tools"]
      assert Map.has_key?(tool_def, "functionDeclarations")
      assert is_list(tool_def["functionDeclarations"])
    end

    test "encode_body strips atom-keyed forbidden fields from tool schemas" do
      {:ok, model} = ReqLLM.model("google:gemini-1.5-flash")
      context = context_fixture()

      tool =
        ReqLLM.Tool.new!(
          name: "register_company",
          description: "Register company",
          parameter_schema: %{
            :type => :object,
            :required => [:company],
            :properties => %{
              :company => %{
                :type => :object,
                :required => [:name, :address],
                :properties => %{
                  :name => %{type: :string},
                  :address => %{
                    :type => :object,
                    :required => [:city],
                    :properties => %{city: %{type: :string}},
                    :additionalProperties => false
                  }
                },
                :additionalProperties => false
              }
            },
            :additionalProperties => false,
            :"$schema" => "https://json-schema.org/draft/2020-12/schema"
          },
          callback: fn _ -> {:ok, %{}} end
        )

      mock_request = %Req.Request{
        options: [
          context: context,
          model: model.model,
          stream: false,
          tools: [tool],
          operation: :chat
        ]
      }

      updated_request = Google.encode_body(mock_request)
      decoded = Jason.decode!(updated_request.body)
      [tool_def] = decoded["tools"]
      [function_def] = tool_def["functionDeclarations"]
      parameters = function_def["parameters"]
      company = parameters["properties"]["company"]
      address = company["properties"]["address"]

      refute Map.has_key?(parameters, "$schema")
      refute Map.has_key?(parameters, "additionalProperties")
      refute Map.has_key?(company, "additionalProperties")
      refute Map.has_key?(address, "additionalProperties")
    end

    test "encode_body with empty tools list and tool_choice omits toolConfig" do
      {:ok, model} = ReqLLM.model("google:gemini-1.5-flash")
      context = context_fixture()

      # Create a mock request mimicking Jido AI's default behavior
      mock_request = %Req.Request{
        options: [
          context: context,
          model: model.model,
          stream: false,
          tools: [],
          tool_choice: :auto,
          operation: :chat
        ]
      }

      updated_request = Google.encode_body(mock_request)
      decoded = Jason.decode!(updated_request.body)

      # Ensure both tools and toolConfig are completely omitted when tools is empty
      refute Map.has_key?(decoded, "tools"), "tools array should be omitted"

      refute Map.has_key?(decoded, "toolConfig"),
             "toolConfig should not be attached if there are no tools"
    end

    test "encode_body includes tool_result name and structured response" do
      {:ok, model} = ReqLLM.model("google:gemini-1.5-flash")

      tool_result =
        Context.tool_result_message(
          "get_weather",
          "call_1",
          %ReqLLM.ToolResult{output: %{"temperature" => 72}}
        )

      context =
        Context.new([
          Context.user("What's the weather?"),
          Context.assistant("",
            tool_calls: [
              %ReqLLM.ToolCall{
                id: "call_1",
                type: "function",
                function: %{name: "get_weather", arguments: ~s({"location":"SF"})}
              }
            ]
          ),
          tool_result
        ])

      mock_request = %Req.Request{
        options: [
          context: context,
          model: model.model,
          stream: false,
          operation: :chat
        ]
      }

      updated_request = Google.encode_body(mock_request)
      decoded = Jason.decode!(updated_request.body)

      tool_parts =
        decoded["contents"]
        |> Enum.flat_map(& &1["parts"])
        |> Enum.filter(&Map.has_key?(&1, "functionResponse"))

      [tool_part] = tool_parts
      function_response = tool_part["functionResponse"]
      assert function_response["name"] == "get_weather"
      assert function_response["response"]["temperature"] == 72
    end

    test "encode_body excludes id from functionCall parts" do
      {:ok, model} = ReqLLM.model("google:gemini-1.5-flash")

      context =
        Context.new([
          Context.user("What's the weather?"),
          Context.assistant("",
            tool_calls: [
              %ReqLLM.ToolCall{
                id: "call_1",
                type: "function",
                function: %{name: "get_weather", arguments: ~s({"location":"SF"})}
              }
            ]
          )
        ])

      mock_request = %Req.Request{
        options: [
          context: context,
          model: model.model,
          stream: false,
          operation: :chat
        ]
      }

      updated_request = Google.encode_body(mock_request)
      decoded = Jason.decode!(updated_request.body)

      function_call_parts =
        decoded["contents"]
        |> Enum.flat_map(& &1["parts"])
        |> Enum.filter(&Map.has_key?(&1, "functionCall"))

      assert [function_call_part] = function_call_parts
      assert function_call_part["functionCall"]["name"] == "get_weather"
      assert function_call_part["functionCall"]["args"] == %{"location" => "SF"}

      refute Map.has_key?(function_call_part["functionCall"], "id"),
             "functionCall must not include 'id' — Google API rejects unknown fields"
    end

    test "encode_body with Google-specific options" do
      {:ok, model} = ReqLLM.model("google:gemini-1.5-flash")
      context = context_fixture()

      safety_settings = [
        %{
          "category" => "HARM_CATEGORY_HARASSMENT",
          "threshold" => "BLOCK_MEDIUM_AND_ABOVE"
        }
      ]

      mock_request = %Req.Request{
        options: [
          context: context,
          model: model.model,
          stream: false,
          google_safety_settings: safety_settings,
          google_candidate_count: 2,
          temperature: 0.8,
          max_tokens: 500
        ]
      }

      updated_request = Google.encode_body(mock_request)
      decoded = Jason.decode!(updated_request.body)

      # Check safety settings
      assert decoded["safetySettings"] == safety_settings

      # Check generation config with Google parameter names
      gen_config = decoded["generationConfig"]
      assert gen_config["temperature"] == 0.8
      assert gen_config["maxOutputTokens"] == 500
      assert gen_config["candidateCount"] == 2
    end

    test "encode_body includes thinkingLevel in generationConfig" do
      context = context_fixture()

      mock_request = %Req.Request{
        options: [
          context: context,
          model: "gemini-3-flash",
          stream: false,
          google_thinking_level: :medium
        ]
      }

      updated_request = Google.encode_body(mock_request)
      decoded = Jason.decode!(updated_request.body)

      thinking_config = decoded["generationConfig"]["thinkingConfig"]
      assert thinking_config["thinkingLevel"] == "medium"
      assert thinking_config["includeThoughts"] == true
      refute Map.has_key?(thinking_config, "thinkingBudget")
    end

    test "encode_body includes thinkingBudget in generationConfig" do
      context = context_fixture()

      mock_request = %Req.Request{
        options: [
          context: context,
          model: "gemini-2.5-flash",
          stream: false,
          google_thinking_budget: 8_192
        ]
      }

      updated_request = Google.encode_body(mock_request)
      decoded = Jason.decode!(updated_request.body)

      thinking_config = decoded["generationConfig"]["thinkingConfig"]
      assert thinking_config["thinkingBudget"] == 8_192
      assert thinking_config["includeThoughts"] == true
      refute Map.has_key?(thinking_config, "thinkingLevel")
    end

    test "encode_body raises when both google_thinking_level and google_thinking_budget are set" do
      context = context_fixture()

      mock_request = %Req.Request{
        options: [
          context: context,
          model: "gemini-3-flash",
          stream: false,
          google_thinking_level: :high,
          google_thinking_budget: 8_192
        ]
      }

      assert_raise ArgumentError,
                   ~r/google_thinking_budget and google_thinking_level cannot be combined/,
                   fn -> Google.encode_body(mock_request) end
    end

    test "encode_body for embedding operation" do
      mock_request = %Req.Request{
        options: [
          operation: :embedding,
          id: "gemini-embedding-001",
          text: "Hello, world!",
          dimensions: 768
        ]
      }

      updated_request = Google.encode_body(mock_request)
      decoded = Jason.decode!(updated_request.body)

      # Check embedding-specific structure
      assert decoded["model"] == "models/gemini-embedding-001"
      assert decoded["content"]["parts"] == [%{"text" => "Hello, world!"}]
      assert decoded["outputDimensionality"] == 768
    end
  end

  describe "labels option" do
    test "encode_body includes labels as a top-level field in chat body" do
      {:ok, model} = ReqLLM.model("google:gemini-1.5-flash")
      context = context_fixture()

      labels = %{"team" => "engineering", "environment" => "production"}

      mock_request = %Req.Request{
        options: [
          context: context,
          model: model.model,
          stream: false,
          labels: labels
        ]
      }

      updated_request = Google.encode_body(mock_request)
      decoded = Jason.decode!(updated_request.body)

      # labels must be top-level, not nested in generationConfig
      assert decoded["labels"] == labels
      refute Map.has_key?(decoded["generationConfig"] || %{}, "labels")
    end

    test "encode_body omits labels when not provided" do
      {:ok, model} = ReqLLM.model("google:gemini-1.5-flash")
      context = context_fixture()

      mock_request = %Req.Request{
        options: [
          context: context,
          model: model.model,
          stream: false
        ]
      }

      updated_request = Google.encode_body(mock_request)
      decoded = Jason.decode!(updated_request.body)

      refute Map.has_key?(decoded, "labels")
    end

    test "encode_body includes labels in object (JSON mode) body" do
      {:ok, model} = ReqLLM.model("google:gemini-1.5-flash")
      context = context_fixture()
      {:ok, schema} = ReqLLM.Schema.compile(name: [type: :string, required: true])

      labels = %{"use_case" => "extraction", "team" => "data"}

      mock_request = %Req.Request{
        options: [
          context: context,
          model: model.model,
          operation: :object,
          compiled_schema: schema,
          max_tokens: 500,
          labels: labels
        ]
      }

      updated_request = Google.encode_body(mock_request)
      decoded = Jason.decode!(updated_request.body)

      assert decoded["labels"] == labels
    end
  end

  describe "google_url_context option" do
    test "encode_body includes url_context tool when boolean true" do
      {:ok, model} = ReqLLM.model("google:gemini-2.0-flash")
      context = context_fixture()

      mock_request = %Req.Request{
        options: [
          context: context,
          model: model.model,
          stream: false,
          google_url_context: true
        ]
      }

      updated_request = Google.encode_body(mock_request)
      decoded = Jason.decode!(updated_request.body)

      assert is_list(decoded["tools"])
      assert length(decoded["tools"]) == 1
      [tool] = decoded["tools"]
      assert Map.has_key?(tool, "url_context")
      assert tool["url_context"] == %{}
    end

    test "encode_body includes url_context tool with map options" do
      {:ok, model} = ReqLLM.model("google:gemini-2.0-flash")
      context = context_fixture()

      mock_request = %Req.Request{
        options: [
          context: context,
          model: model.model,
          stream: false,
          google_url_context: %{some_option: "value"}
        ]
      }

      updated_request = Google.encode_body(mock_request)
      decoded = Jason.decode!(updated_request.body)

      assert is_list(decoded["tools"])
      assert length(decoded["tools"]) == 1
      [tool] = decoded["tools"]
      assert Map.has_key?(tool, "url_context")
      assert tool["url_context"] == %{"some_option" => "value"}
    end

    test "encode_body combines url_context with user tools" do
      {:ok, model} = ReqLLM.model("google:gemini-2.0-flash")
      context = context_fixture()

      tool =
        ReqLLM.Tool.new!(
          name: "test_tool",
          description: "A test tool",
          parameter_schema: [
            name: [type: :string, required: true, doc: "A name parameter"]
          ],
          callback: fn _ -> {:ok, "result"} end
        )

      mock_request = %Req.Request{
        options: [
          context: context,
          model: model.model,
          stream: false,
          tools: [tool],
          google_url_context: true,
          operation: :chat
        ]
      }

      updated_request = Google.encode_body(mock_request)
      decoded = Jason.decode!(updated_request.body)

      assert is_list(decoded["tools"])
      assert length(decoded["tools"]) == 2

      tool_types = Enum.map(decoded["tools"], fn t -> Map.keys(t) end) |> List.flatten()
      assert "url_context" in tool_types
      assert "functionDeclarations" in tool_types
    end

    test "encode_body combines url_context with grounding" do
      {:ok, model} = ReqLLM.model("google:gemini-2.0-flash")
      context = context_fixture()

      mock_request = %Req.Request{
        options: [
          context: context,
          model: model.model,
          stream: false,
          google_grounding: %{enable: true},
          google_url_context: true
        ]
      }

      updated_request = Google.encode_body(mock_request)
      decoded = Jason.decode!(updated_request.body)

      assert is_list(decoded["tools"])
      assert length(decoded["tools"]) == 2

      tool_types = Enum.map(decoded["tools"], fn t -> Map.keys(t) end) |> List.flatten()
      assert "url_context" in tool_types
      assert "google_search" in tool_types
    end
  end

  describe "response decoding" do
    test "decode_response handles non-streaming responses" do
      # Create a mock Google response
      google_response = %{
        "candidates" => [
          %{
            "content" => %{
              "parts" => [%{"text" => "Hello there! How can I help you today?"}],
              "role" => "model"
            },
            "finishReason" => "STOP",
            "safetyRatings" => []
          }
        ],
        "usageMetadata" => %{
          "promptTokenCount" => 10,
          "candidatesTokenCount" => 15,
          "totalTokenCount" => 25
        }
      }

      # Create a mock Req response
      mock_resp = %Req.Response{
        status: 200,
        body: google_response
      }

      {:ok, model} = ReqLLM.model("google:gemini-1.5-flash")
      context = context_fixture()

      mock_req = %Req.Request{
        options: [context: context, stream: false, model: model.model]
      }

      # Test decode_response directly
      {req, resp} = Google.decode_response({mock_req, mock_resp})

      assert req == mock_req
      assert %ReqLLM.Response{} = resp.body

      response = resp.body
      assert is_binary(response.id)
      assert response.model == model.model
      assert response.stream? == false

      # Verify message normalization
      assert response.message.role == :assistant
      text = ReqLLM.Response.text(response)
      assert text == "Hello there! How can I help you today?"
      assert response.finish_reason == :stop

      # Verify usage normalization
      assert response.usage.input_tokens == 10
      assert response.usage.output_tokens == 15
      assert response.usage.total_tokens == 25

      # Verify context advancement (original + assistant)
      assert length(response.context.messages) == 3
      assert List.last(response.context.messages).role == :assistant
    end

    test "decode_response extracts cached token counts" do
      # Create a mock Google response with cached tokens
      google_response = %{
        "candidates" => [
          %{
            "content" => %{
              "parts" => [%{"text" => "Response using cached context"}],
              "role" => "model"
            },
            "finishReason" => "STOP"
          }
        ],
        "usageMetadata" => %{
          "promptTokenCount" => 1500,
          "candidatesTokenCount" => 50,
          "totalTokenCount" => 1550,
          "cachedContentTokenCount" => 1200
        }
      }

      mock_resp = %Req.Response{
        status: 200,
        body: google_response
      }

      {:ok, model} = ReqLLM.model("google:gemini-2.5-flash")
      context = context_fixture()

      mock_req = %Req.Request{
        options: [context: context, stream: false, model: model.model]
      }

      {_req, resp} = Google.decode_response({mock_req, mock_resp})

      assert %ReqLLM.Response{} = resp.body
      response = resp.body

      # Verify cached tokens are extracted
      assert response.usage.cached_tokens == 1200
      assert response.usage.input_tokens == 1500
      assert response.usage.output_tokens == 50
      assert response.usage.total_tokens == 1550
    end

    test "decode_response includes thought tokens in output usage" do
      google_response = %{
        "candidates" => [
          %{
            "content" => %{
              "parts" => [%{"text" => "The answer is 42."}],
              "role" => "model"
            },
            "finishReason" => "STOP"
          }
        ],
        "usageMetadata" => %{
          "promptTokenCount" => 10,
          "candidatesTokenCount" => 15,
          "thoughtsTokenCount" => 5,
          "totalTokenCount" => 30
        }
      }

      mock_resp = %Req.Response{
        status: 200,
        body: google_response
      }

      {:ok, model} = ReqLLM.model("google:gemini-2.5-flash")
      context = context_fixture()

      mock_req = %Req.Request{
        options: [context: context, stream: false, model: model.model]
      }

      {_req, resp} = Google.decode_response({mock_req, mock_resp})

      assert %ReqLLM.Response{} = resp.body
      response = resp.body
      assert response.usage.input_tokens == 10
      assert response.usage.output_tokens == 20
      assert response.usage.total_tokens == 30
      assert response.usage.reasoning_tokens == 5
    end

    test "decode_response preserves tool calls" do
      google_response = %{
        "candidates" => [
          %{
            "content" => %{
              "parts" => [
                %{
                  "functionCall" => %{
                    "name" => "demo_tool",
                    "args" => %{"payload" => "value"},
                    "id" => "call-1"
                  }
                }
              ],
              "role" => "model"
            },
            "finishReason" => "STOP"
          }
        ],
        "usageMetadata" => %{
          "promptTokenCount" => 12,
          "candidatesTokenCount" => 3,
          "totalTokenCount" => 15
        }
      }

      mock_resp = %Req.Response{status: 200, body: google_response}

      context = context_fixture()

      mock_req = %Req.Request{
        options: [context: context, stream: false, id: "gemini-1.5-flash"]
      }

      {_req, resp} = Google.decode_response({mock_req, mock_resp})

      response = resp.body
      assert %ReqLLM.Response{} = response

      assert [tool_call] = ReqLLM.Response.tool_calls(response)
      assert tool_call.function.name == "demo_tool"
      assert tool_call.id == "call-1"
      # Bug #271 fix: Google returns "STOP" even with tool calls, but we correctly
      # detect tool calls and return :tool_calls finish_reason
      assert ReqLLM.Response.finish_reason(response) == :tool_calls
    end

    test "decode_response handles streaming responses" do
      # Create mock streaming chunks (Google format)
      stream_chunks = [
        %{"candidates" => [%{"content" => %{"parts" => [%{"text" => "Hello"}]}}]},
        %{"candidates" => [%{"content" => %{"parts" => [%{"text" => " world"}]}}]},
        %{"candidates" => [%{"finishReason" => "STOP"}]}
      ]

      # Create a mock stream
      mock_stream = Stream.map(stream_chunks, & &1)

      # Create a mock Req response with streaming body
      mock_resp = %Req.Response{
        status: 200,
        body: mock_stream
      }

      # Create a mock request with context and model
      context = context_fixture()
      model = "gemini-1.5-flash"

      mock_req = %Req.Request{
        options: [context: context, stream: true, model: model]
      }

      # Test decode_response directly
      {req, resp} = Google.decode_response({mock_req, mock_resp})

      assert req == mock_req
      assert %ReqLLM.Response{} = resp.body

      response = resp.body
      assert response.stream? == true
      assert is_struct(response.stream, Stream)
      assert response.model == model

      # Verify context is preserved (original messages only in streaming)
      assert length(response.context.messages) == 2

      # Verify stream structure and processing
      assert response.usage == %{
               input_tokens: 0,
               output_tokens: 0,
               total_tokens: 0,
               cached_tokens: 0,
               reasoning_tokens: 0
             }

      assert response.finish_reason == nil
      assert response.provider_meta == %{}
    end

    test "decode_response for embedding returns normalized OpenAI format" do
      embedding_response = %{
        "embedding" => %{
          "values" => [0.1, -0.2, 0.3, 0.4, -0.5]
        },
        "usageMetadata" => %{
          "promptTokenCount" => 2,
          "totalTokenCount" => 2
        }
      }

      mock_resp = %Req.Response{
        status: 200,
        body: embedding_response
      }

      mock_req = %Req.Request{
        options: [operation: :embedding, id: "gemini-embedding-001"]
      }

      {req, resp} = Google.decode_response({mock_req, mock_resp})

      assert req == mock_req

      assert resp.body == %{
               "data" => [%{"index" => 0, "embedding" => [0.1, -0.2, 0.3, 0.4, -0.5]}],
               "usageMetadata" => %{
                 "promptTokenCount" => 2,
                 "totalTokenCount" => 2
               }
             }
    end

    test "decode_response handles API errors with non-200 status" do
      # Create error response
      error_body = %{
        "error" => %{
          "code" => 400,
          "message" => "Invalid API key",
          "status" => "INVALID_ARGUMENT"
        }
      }

      mock_resp = %Req.Response{
        status: 400,
        body: error_body
      }

      context = context_fixture()

      mock_req = %Req.Request{
        options: [context: context, id: "gemini-1.5-flash"]
      }

      # Test decode_response error handling
      {req, error} = Google.decode_response({mock_req, mock_resp})

      assert req == mock_req
      assert %ReqLLM.Error.API.Response{} = error
      assert error.status == 400
      assert error.reason == "Google API error"
      assert error.response_body == error_body
    end
  end

  describe "decode_stream_event/2 finish_reason capture" do
    setup do
      {:ok, model: %LLMDB.Model{id: "gemini-2.5-flash", provider: :google}}
    end

    test "finishReason on candidate without content.parts + usageMetadata", %{model: model} do
      event = %{
        data: %{
          "candidates" => [%{"finishReason" => "STOP", "index" => 0}],
          "usageMetadata" => %{
            "promptTokenCount" => 10,
            "candidatesTokenCount" => 5,
            "totalTokenCount" => 15
          }
        }
      }

      [meta_chunk] = Google.decode_stream_event(event, model)
      assert meta_chunk.type == :meta
      assert meta_chunk.metadata[:finish_reason] == "stop"
      assert meta_chunk.metadata[:terminal?] == true
      assert meta_chunk.metadata[:usage][:input_tokens] == 10
    end

    test "streaming usageMetadata includes thought tokens in output usage", %{model: model} do
      event = %{
        data: %{
          "candidates" => [%{"finishReason" => "STOP", "index" => 0}],
          "usageMetadata" => %{
            "promptTokenCount" => 10,
            "candidatesTokenCount" => 15,
            "thoughtsTokenCount" => 5,
            "totalTokenCount" => 30
          }
        }
      }

      [meta_chunk] = Google.decode_stream_event(event, model)
      assert meta_chunk.type == :meta
      assert meta_chunk.metadata[:usage][:input_tokens] == 10
      assert meta_chunk.metadata[:usage][:output_tokens] == 20
      assert meta_chunk.metadata[:usage][:total_tokens] == 30
      assert meta_chunk.metadata[:usage][:reasoning_tokens] == 5
    end

    test "finishReason on candidate without content.parts (no usage)", %{model: model} do
      event = %{data: %{"candidates" => [%{"finishReason" => "STOP", "index" => 0}]}}

      [meta_chunk] = Google.decode_stream_event(event, model)
      assert meta_chunk.type == :meta
      assert meta_chunk.metadata[:finish_reason] == "stop"
      assert meta_chunk.metadata[:terminal?] == true
    end

    test "finishReason on candidate with content role but no parts key", %{model: model} do
      event = %{
        data: %{
          "candidates" => [
            %{"content" => %{"role" => "model"}, "finishReason" => "STOP", "index" => 0}
          ],
          "usageMetadata" => %{"promptTokenCount" => 1, "totalTokenCount" => 1}
        }
      }

      [meta_chunk] = Google.decode_stream_event(event, model)
      assert meta_chunk.type == :meta
      assert meta_chunk.metadata[:finish_reason] == "stop"
      assert meta_chunk.metadata[:terminal?] == true
    end

    test "usageMetadata alone still has no finish_reason (unchanged)", %{model: model} do
      event = %{
        data: %{
          "usageMetadata" => %{"promptTokenCount" => 1, "totalTokenCount" => 1}
        }
      }

      [meta_chunk] = Google.decode_stream_event(event, model)
      assert meta_chunk.type == :meta
      refute Map.has_key?(meta_chunk.metadata, :finish_reason)
      assert meta_chunk.metadata[:terminal?] == true
    end

    test "content.parts + finishReason still preferred (regression)", %{model: model} do
      event = %{
        data: %{
          "candidates" => [
            %{
              "content" => %{"parts" => [%{"text" => "hello"}], "role" => "model"},
              "finishReason" => "STOP"
            }
          ],
          "usageMetadata" => %{"promptTokenCount" => 2, "totalTokenCount" => 2}
        }
      }

      chunks = Google.decode_stream_event(event, model)
      assert Enum.any?(chunks, &match?(%{type: :content, text: "hello"}, &1))
      meta_chunk = Enum.find(chunks, &(&1.type == :meta))
      assert meta_chunk.metadata[:finish_reason] == "stop"
    end
  end

  describe "option translation" do
    test "provider implements translate_options/3" do
      # Google implements translate_options/3 for stream? alias handling
      assert function_exported?(Google, :translate_options, 3)
    end

    test "translate_options handles stream? alias" do
      {:ok, model} = ReqLLM.model("google:gemini-1.5-flash")

      # Test stream? -> stream translation
      opts = [temperature: 0.7, stream?: true]
      {translated_opts, warnings} = Google.translate_options(:chat, model, opts)

      assert Keyword.get(translated_opts, :stream) == true
      refute Keyword.has_key?(translated_opts, :stream?)
      assert warnings == []
    end

    test "provider-specific option handling" do
      # Test that provider-specific options are present in the provider schema
      schema_keys = Google.provider_schema().schema |> Keyword.keys()

      # Test that these options are supported
      supported_opts = Google.supported_provider_options()

      for provider_option <- schema_keys do
        assert provider_option in supported_opts,
               "Expected #{provider_option} to be in supported options"
      end
    end

    test "translate_options maps reasoning_effort to google_thinking_budget" do
      {:ok, model} = ReqLLM.model("google:gemini-1.5-flash")

      test_cases = [
        {:none, 0},
        {:minimal, 2_048},
        {:low, 4_096},
        {:medium, 8_192},
        {:high, 16_384},
        {:xhigh, 32_768}
      ]

      for {effort, expected_budget} <- test_cases do
        opts = [reasoning_effort: effort]
        {translated_opts, _warnings} = Google.translate_options(:chat, model, opts)

        assert Keyword.get(translated_opts, :google_thinking_budget) == expected_budget,
               "Expected reasoning_effort #{inspect(effort)} to map to budget #{expected_budget}"
      end
    end

    test "translate_options maps reasoning_effort to google_thinking_level for Gemini 3 models" do
      {:ok, model} = ReqLLM.model(%{provider: :google, id: "gemini-3-flash"})

      test_cases = [
        {:none, :minimal},
        {:minimal, :minimal},
        {:low, :low},
        {:medium, :medium},
        {:high, :high},
        {:xhigh, :high}
      ]

      for {effort, expected_level} <- test_cases do
        opts = [reasoning_effort: effort]
        {translated_opts, _warnings} = Google.translate_options(:chat, model, opts)

        assert Keyword.get(translated_opts, :google_thinking_level) == expected_level,
               "Expected reasoning_effort #{inspect(effort)} to map to level #{inspect(expected_level)}"

        assert Keyword.get(translated_opts, :google_thinking_budget) == nil,
               "Expected no google_thinking_budget for Gemini 3 model"
      end
    end

    test "translate_options uses google_thinking_budget for reasoning_token_budget even on Gemini 3" do
      {:ok, model} = ReqLLM.model(%{provider: :google, id: "gemini-3-flash"})

      opts = [reasoning_token_budget: 10_000]
      {translated_opts, _warnings} = Google.translate_options(:chat, model, opts)

      assert Keyword.get(translated_opts, :google_thinking_budget) == 10_000
      assert Keyword.get(translated_opts, :google_thinking_level) == nil
    end
  end

  describe "usage extraction" do
    test "extract_usage with valid usage data" do
      {:ok, model} = ReqLLM.model("google:gemini-1.5-flash")

      body_with_usage = %{
        "usageMetadata" => %{
          "promptTokenCount" => 10,
          "candidatesTokenCount" => 20,
          "totalTokenCount" => 30
        }
      }

      {:ok, usage} = Google.extract_usage(body_with_usage, model)
      assert usage.input_tokens == 10
      assert usage.output_tokens == 20
      assert usage.total_tokens == 30
    end

    test "extract_usage with missing usage data" do
      {:ok, model} = ReqLLM.model("google:gemini-1.5-flash")
      body_without_usage = %{"candidates" => []}

      {:error, :no_usage_found} = Google.extract_usage(body_without_usage, model)
    end

    test "extract_usage with invalid body type" do
      {:ok, model} = ReqLLM.model("google:gemini-1.5-flash")

      {:error, :invalid_body} = Google.extract_usage("invalid", model)
      {:error, :invalid_body} = Google.extract_usage(nil, model)
      {:error, :invalid_body} = Google.extract_usage(123, model)
    end

    test "extract_usage with cached content tokens" do
      {:ok, model} = ReqLLM.model("google:gemini-1.5-flash")

      body_with_cached_tokens = %{
        "usageMetadata" => %{
          "promptTokenCount" => 500,
          "candidatesTokenCount" => 200,
          "totalTokenCount" => 700,
          "cachedContentTokenCount" => 120
        }
      }

      {:ok, usage} = Google.extract_usage(body_with_cached_tokens, model)
      assert usage.input_tokens == 500
      assert usage.output_tokens == 200
      assert usage.total_tokens == 700
      assert usage.cached_tokens == 120
    end

    test "extract_usage without cached content tokens" do
      {:ok, model} = ReqLLM.model("google:gemini-1.5-flash")

      body_without_cached_tokens = %{
        "usageMetadata" => %{
          "promptTokenCount" => 10,
          "candidatesTokenCount" => 20,
          "totalTokenCount" => 30
        }
      }

      {:ok, usage} = Google.extract_usage(body_without_cached_tokens, model)
      assert usage.input_tokens == 10
      assert usage.output_tokens == 20
      assert usage.total_tokens == 30
      assert usage.cached_tokens == 0
    end

    test "extract_usage includes image usage when inline data is present" do
      model = %LLMDB.Model{provider: :google, id: "gemini-2.5-flash-image"}

      body_with_image = %{
        "candidates" => [
          %{
            "content" => %{
              "parts" => [
                %{
                  "inlineData" => %{
                    "mimeType" => "image/png",
                    "data" => "AAA"
                  }
                }
              ]
            }
          }
        ]
      }

      {:ok, usage} = Google.extract_usage(body_with_image, model)
      assert usage.image_usage.generated.count == 1
    end

    test "extract_usage counts web searches when pricing unit is query" do
      model = %LLMDB.Model{
        id: "gemini-query-billed",
        provider: :google,
        pricing: %{
          components: [
            %{
              id: "tool.web_search",
              kind: "tool",
              tool: "web_search",
              unit: "query",
              per: 1000,
              rate: 14.0
            }
          ]
        }
      }

      body = %{
        "usageMetadata" => %{
          "promptTokenCount" => 10,
          "candidatesTokenCount" => 20,
          "totalTokenCount" => 30
        },
        "candidates" => [
          %{
            "groundingMetadata" => %{
              "webSearchQueries" => ["q1", "q2"]
            }
          }
        ]
      }

      {:ok, usage} = Google.extract_usage(body, model)
      assert usage[:tool_usage][:web_search][:count] == 2
      assert usage[:tool_usage][:web_search][:unit] == "query"
    end

    test "extract_usage defaults web search count to 1 when pricing unit is not query" do
      model = %LLMDB.Model{
        id: "gemini-2.5-flash",
        provider: :google,
        pricing: %{
          components: [
            %{
              id: "tool.web_search",
              kind: "tool",
              tool: "web_search",
              unit: "call",
              per: 1000,
              rate: 35.0
            }
          ]
        }
      }

      body = %{
        "usageMetadata" => %{
          "promptTokenCount" => 10,
          "candidatesTokenCount" => 20,
          "totalTokenCount" => 30
        },
        "candidates" => [
          %{
            "groundingMetadata" => %{
              "webSearchQueries" => ["q1", "q2"]
            }
          }
        ]
      }

      {:ok, usage} = Google.extract_usage(body, model)
      assert usage[:tool_usage][:web_search][:count] == 1
      assert usage[:tool_usage][:web_search][:unit] == "call"
    end

    test "extract_usage handles missing web search pricing component" do
      model = %LLMDB.Model{id: "gemini-no-pricing", provider: :google, pricing: nil}

      body = %{
        "usageMetadata" => %{
          "promptTokenCount" => 10,
          "candidatesTokenCount" => 20,
          "totalTokenCount" => 30
        },
        "candidates" => [
          %{
            "groundingMetadata" => %{
              "webSearchQueries" => ["q1", "q2"]
            }
          }
        ]
      }

      {:ok, usage} = Google.extract_usage(body, model)
      assert usage[:tool_usage][:web_search][:count] == 1
      assert usage[:tool_usage][:web_search][:unit] == :call
    end
  end

  describe "object generation with native JSON mode" do
    test "prepare_request for :object with low max_tokens gets adjusted" do
      {:ok, model} = ReqLLM.model("google:gemini-1.5-flash")
      context = context_fixture()
      {:ok, schema} = ReqLLM.Schema.compile(name: [type: :string, required: true])

      opts = [max_tokens: 50, compiled_schema: schema]
      {:ok, request} = Google.prepare_request(:object, model, context, opts)

      assert request.options[:max_tokens] == 200
    end

    test "prepare_request for :object with nil max_tokens gets default" do
      {:ok, model} = ReqLLM.model("google:gemini-1.5-flash")
      context = context_fixture()
      {:ok, schema} = ReqLLM.Schema.compile([])

      opts = [compiled_schema: schema]
      {:ok, request} = Google.prepare_request(:object, model, context, opts)

      assert request.options[:max_tokens] == 4096
    end

    test "prepare_request for :object with sufficient max_tokens unchanged" do
      {:ok, model} = ReqLLM.model("google:gemini-1.5-flash")
      context = context_fixture()
      {:ok, schema} = ReqLLM.Schema.compile(value: [type: :integer])

      opts = [max_tokens: 1000, compiled_schema: schema]
      {:ok, request} = Google.prepare_request(:object, model, context, opts)

      assert request.options[:max_tokens] == 1000
    end

    test "prepare_request for :object rejects tools" do
      {:ok, model} = ReqLLM.model("google:gemini-1.5-flash")
      context = context_fixture()
      {:ok, schema} = ReqLLM.Schema.compile(name: [type: :string])

      tool =
        ReqLLM.Tool.new!(
          name: "test_tool",
          description: "A test",
          parameter_schema: [],
          callback: fn _ -> {:ok, "ok"} end
        )

      opts = [compiled_schema: schema, tools: [tool]]
      {:error, error} = Google.prepare_request(:object, model, context, opts)

      assert %ReqLLM.Error.Invalid.Parameter{} = error
      assert error.parameter =~ "tools are not supported"
    end

    test "encode_object_body creates JSON mode request" do
      {:ok, model} = ReqLLM.model("google:gemini-1.5-flash")
      context = context_fixture()
      {:ok, schema} = ReqLLM.Schema.compile(name: [type: :string, required: true])

      mock_request = %Req.Request{
        options: [
          context: context,
          model: model.model,
          operation: :object,
          compiled_schema: schema,
          max_tokens: 500,
          provider_options: [google_api_version: "v1beta"]
        ]
      }

      updated_request = Google.encode_body(mock_request)
      decoded = Jason.decode!(updated_request.body)

      assert decoded["generationConfig"]["responseMimeType"] == "application/json"
      assert decoded["generationConfig"]["candidateCount"] == 1
      assert Map.has_key?(decoded["generationConfig"], "responseSchema")
      refute Map.has_key?(decoded, "tools")
      refute Map.has_key?(decoded, "toolConfig")
    end

    test "encode_object_body uses responseSchema for non-2.5 models" do
      context = context_fixture()

      {:ok, schema} =
        ReqLLM.Schema.compile(
          name: [type: :string, required: true],
          age: [type: :pos_integer]
        )

      mock_request = %Req.Request{
        options: [
          context: context,
          id: "gemini-1.5-flash",
          operation: :object,
          compiled_schema: schema
        ]
      }

      updated_request = Google.encode_body(mock_request)
      decoded = Jason.decode!(updated_request.body)

      response_schema = decoded["generationConfig"]["responseSchema"]
      assert response_schema["type"] == "OBJECT"
      assert Map.has_key?(response_schema, "properties")
      assert Map.has_key?(response_schema["properties"], "name")
      assert response_schema["properties"]["name"]["type"] == "STRING"
      refute Map.has_key?(decoded["generationConfig"], "responseJsonSchema")
    end

    test "encode_object_body uses responseJsonSchema for Gemini 2.5" do
      context = context_fixture()

      {:ok, schema} =
        ReqLLM.Schema.compile(
          name: [type: :string, required: true],
          age: [type: :pos_integer]
        )

      mock_request = %Req.Request{
        options: [
          context: context,
          model: "gemini-2.5-flash",
          operation: :object,
          compiled_schema: schema
        ]
      }

      updated_request = Google.encode_body(mock_request)
      decoded = Jason.decode!(updated_request.body)

      response_json_schema = decoded["generationConfig"]["responseJsonSchema"]
      assert response_json_schema["type"] == "object"
      assert Map.has_key?(response_json_schema, "properties")
      refute Map.has_key?(decoded["generationConfig"], "responseSchema")
    end

    test "encode_object_body uses responseJsonSchema for Gemini 3.1" do
      context = context_fixture()

      {:ok, schema} =
        ReqLLM.Schema.compile(
          name: [type: :string, required: true],
          age: [type: :pos_integer]
        )

      mock_request = %Req.Request{
        options: [
          context: context,
          model: "gemini-3.1-pro-preview",
          operation: :object,
          compiled_schema: schema
        ]
      }

      updated_request = Google.encode_body(mock_request)
      decoded = Jason.decode!(updated_request.body)

      response_json_schema = decoded["generationConfig"]["responseJsonSchema"]
      assert response_json_schema["type"] == "object"
      assert Map.has_key?(response_json_schema, "properties")
      refute Map.has_key?(decoded["generationConfig"], "responseSchema")
    end

    test "prepare_request creates configured embedding request" do
      {:ok, model} = ReqLLM.model("google:gemini-embedding-001")
      text = "Hello, world!"
      opts = [dimensions: 768]

      {:ok, request} = Google.prepare_request(:embedding, model, text, opts)

      assert request.method == :post
      assert request.url.path == "/models/gemini-embedding-001:embedContent"

      # Check request options contain embedding-specific data
      assert request.options[:text] == text
      assert request.options[:operation] == :embedding
      assert request.options[:dimensions] == 768
    end

    test "prepare_request rejects unsupported operations" do
      {:ok, model} = ReqLLM.model("google:gemini-1.5-flash")
      context = context_fixture()

      # Test unsupported operation for 3-arg version
      {:error, error} = Google.prepare_request(:unsupported_operation, model, context, [])
      assert %ReqLLM.Error.Invalid.Parameter{} = error
      assert error.parameter =~ "operation: :unsupported_operation not supported"

      # Test unsupported operation for object with schema
      {:ok, schema} = ReqLLM.Schema.compile([])

      {:error, error} =
        Google.prepare_request(:unsupported_operation, model, context, compiled_schema: schema)

      assert %ReqLLM.Error.Invalid.Parameter{} = error
      assert error.parameter =~ "operation: :unsupported_operation not supported"
    end
  end

  describe "error handling & robustness" do
    test "context validation allows multiple system messages" do
      context =
        Context.new([
          Context.system("System 1"),
          Context.system("System 2"),
          Context.user("Hello")
        ])

      assert ^context = Context.validate!(context)
    end
  end

  describe "file attachment support" do
    test "encode_body handles :file ContentPart with inline_data format" do
      file_content = "test file content"

      file_part = %ReqLLM.Message.ContentPart{
        type: :file,
        data: file_content,
        media_type: "application/pdf"
      }

      message_with_file = %ReqLLM.Message{
        role: :user,
        content: [file_part]
      }

      context = %ReqLLM.Context{messages: [message_with_file]}

      mock_request = %Req.Request{
        options: [
          context: context,
          id: "gemini-1.5-flash",
          stream: false
        ]
      }

      updated_request = Google.encode_body(mock_request)
      decoded = Jason.decode!(updated_request.body)

      [user_msg] = decoded["contents"]
      parts = user_msg["parts"]

      assert length(parts) == 1, "Expected 1 part, got: #{inspect(parts)}"
      [part] = parts

      assert Map.has_key?(part, "inline_data")
      assert part["inline_data"]["mime_type"] == "application/pdf"
      assert Base.decode64!(part["inline_data"]["data"]) == file_content
    end

    test "encode_body handles video ContentPart with inline_data format" do
      video_content = "fake video bytes"

      video_part = %ReqLLM.Message.ContentPart{
        type: :file,
        data: video_content,
        media_type: "video/mp4"
      }

      message_with_video = %ReqLLM.Message{
        role: :user,
        content: [video_part]
      }

      context = %ReqLLM.Context{messages: [message_with_video]}

      mock_request = %Req.Request{
        options: [
          context: context,
          id: "gemini-1.5-flash",
          stream: false
        ]
      }

      updated_request = Google.encode_body(mock_request)
      decoded = Jason.decode!(updated_request.body)

      [user_msg] = decoded["contents"]
      [part] = user_msg["parts"]

      assert Map.has_key?(part, "inline_data")
      assert part["inline_data"]["mime_type"] == "video/mp4"
      assert Base.decode64!(part["inline_data"]["data"]) == video_content
    end

    test "encode_body handles image ContentPart with inline_data format" do
      image_content = <<137, 80, 78, 71, 13, 10, 26, 10>>

      image_part = %ReqLLM.Message.ContentPart{
        type: :image,
        data: image_content,
        media_type: "image/png"
      }

      message_with_image = %ReqLLM.Message{
        role: :user,
        content: [image_part]
      }

      context = %ReqLLM.Context{messages: [message_with_image]}

      mock_request = %Req.Request{
        options: [
          context: context,
          id: "gemini-1.5-flash",
          stream: false
        ]
      }

      updated_request = Google.encode_body(mock_request)
      decoded = Jason.decode!(updated_request.body)

      [user_msg] = decoded["contents"]
      [part] = user_msg["parts"]

      assert Map.has_key?(part, "inline_data")
      assert part["inline_data"]["mime_type"] == "image/png"
      assert Base.decode64!(part["inline_data"]["data"]) == image_content
    end
  end

  describe "image_url file URI support" do
    test "encode_body converts HTTPS image_url to fileData format" do
      url = "https://example.com/images/photo.jpg"

      image_url_part = ReqLLM.Message.ContentPart.image_url(url)

      message = %ReqLLM.Message{
        role: :user,
        content: [image_url_part]
      }

      context = %ReqLLM.Context{messages: [message]}

      mock_request = %Req.Request{
        options: [
          context: context,
          id: "gemini-1.5-flash",
          stream: false
        ]
      }

      updated_request = Google.encode_body(mock_request)
      decoded = Jason.decode!(updated_request.body)

      [user_msg] = decoded["contents"]
      [part] = user_msg["parts"]

      assert Map.has_key?(part, "fileData")
      assert part["fileData"]["fileUri"] == url
      assert part["fileData"]["mimeType"] == "image/jpeg"
    end

    test "encode_body converts HTTP image_url to fileData format" do
      url = "http://example.com/images/photo.png"

      image_url_part = ReqLLM.Message.ContentPart.image_url(url)

      message = %ReqLLM.Message{
        role: :user,
        content: [image_url_part]
      }

      context = %ReqLLM.Context{messages: [message]}

      mock_request = %Req.Request{
        options: [
          context: context,
          id: "gemini-1.5-flash",
          stream: false
        ]
      }

      updated_request = Google.encode_body(mock_request)
      decoded = Jason.decode!(updated_request.body)

      [user_msg] = decoded["contents"]
      [part] = user_msg["parts"]

      assert Map.has_key?(part, "fileData")
      assert part["fileData"]["fileUri"] == url
      assert part["fileData"]["mimeType"] == "image/png"
    end

    test "encode_body converts OpenAI-format video_url to fileData format" do
      url = "https://example.com/videos/clip.mp4"

      mock_request = %Req.Request{
        options: [
          messages: [
            %{
              "role" => "user",
              "content" => [
                %{"type" => "text", "text" => "What happens in this video?"},
                %{"type" => "video_url", "video_url" => %{"url" => url}}
              ]
            }
          ],
          id: "gemini-1.5-flash",
          stream: false
        ]
      }

      updated_request = Google.encode_body(mock_request)
      decoded = Jason.decode!(updated_request.body)

      [user_msg] = decoded["contents"]
      [_text_part, part] = user_msg["parts"]

      assert Map.has_key?(part, "fileData")
      assert part["fileData"]["fileUri"] == url
      assert part["fileData"]["mimeType"] == "video/mp4"
    end

    test "encode_body converts GCS URI to fileData format" do
      url = "gs://my-bucket/path/to/document.pdf"

      image_url_part = ReqLLM.Message.ContentPart.image_url(url)

      message = %ReqLLM.Message{
        role: :user,
        content: [image_url_part]
      }

      context = %ReqLLM.Context{messages: [message]}

      mock_request = %Req.Request{
        options: [
          context: context,
          id: "gemini-1.5-flash",
          stream: false
        ]
      }

      updated_request = Google.encode_body(mock_request)
      decoded = Jason.decode!(updated_request.body)

      [user_msg] = decoded["contents"]
      [part] = user_msg["parts"]

      assert Map.has_key?(part, "fileData")
      assert part["fileData"]["fileUri"] == url
      assert part["fileData"]["mimeType"] == "application/pdf"
    end

    test "encode_body infers mime type from URL extension" do
      test_cases = [
        {"https://example.com/file.jpg", "image/jpeg"},
        {"https://example.com/file.jpeg", "image/jpeg"},
        {"https://example.com/file.png", "image/png"},
        {"https://example.com/file.gif", "image/gif"},
        {"https://example.com/file.webp", "image/webp"},
        {"https://example.com/file.pdf", "application/pdf"},
        {"https://example.com/file.mp3", "audio/mpeg"},
        {"https://example.com/file.mp4", "video/mp4"},
        {"https://example.com/file.m4a", "audio/mp4"},
        {"https://example.com/file.wav", "audio/wav"}
      ]

      for {url, expected_mime} <- test_cases do
        image_url_part = ReqLLM.Message.ContentPart.image_url(url)

        message = %ReqLLM.Message{
          role: :user,
          content: [image_url_part]
        }

        context = %ReqLLM.Context{messages: [message]}

        mock_request = %Req.Request{
          options: [
            context: context,
            id: "gemini-1.5-flash",
            stream: false
          ]
        }

        updated_request = Google.encode_body(mock_request)
        decoded = Jason.decode!(updated_request.body)

        [user_msg] = decoded["contents"]
        [part] = user_msg["parts"]

        assert part["fileData"]["mimeType"] == expected_mime,
               "Expected #{expected_mime} for #{url}, got #{part["fileData"]["mimeType"]}"
      end
    end

    test "encode_body omits mimeType for URLs with unrecognizable extensions" do
      url = "https://example.com/file.unknown"
      image_url_part = ReqLLM.Message.ContentPart.image_url(url)

      message = %ReqLLM.Message{role: :user, content: [image_url_part]}
      context = %ReqLLM.Context{messages: [message]}

      mock_request = %Req.Request{
        options: [context: context, id: "gemini-1.5-flash", stream: false]
      }

      updated_request = Google.encode_body(mock_request)
      decoded = Jason.decode!(updated_request.body)

      [user_msg] = decoded["contents"]
      [part] = user_msg["parts"]

      assert part["fileData"]["fileUri"] == url

      refute Map.has_key?(part["fileData"], "mimeType"),
             "mimeType should be omitted for unrecognizable extensions"
    end

    test "encode_body omits mimeType for YouTube URLs" do
      url = "https://www.youtube.com/watch?v=dQw4w9WgXcQ"
      image_url_part = ReqLLM.Message.ContentPart.image_url(url)

      message = %ReqLLM.Message{role: :user, content: [image_url_part]}
      context = %ReqLLM.Context{messages: [message]}

      mock_request = %Req.Request{
        options: [context: context, id: "gemini-1.5-flash", stream: false]
      }

      updated_request = Google.encode_body(mock_request)
      decoded = Jason.decode!(updated_request.body)

      [user_msg] = decoded["contents"]
      [part] = user_msg["parts"]

      assert part["fileData"]["fileUri"] == url

      refute Map.has_key?(part["fileData"], "mimeType"),
             "mimeType should be omitted for YouTube URLs so Gemini can infer it"
    end

    test "encode_body strips query params when inferring mime type from URL" do
      url = "https://example.com/image.jpg?token=abc123&size=large"

      image_url_part = ReqLLM.Message.ContentPart.image_url(url)

      message = %ReqLLM.Message{
        role: :user,
        content: [image_url_part]
      }

      context = %ReqLLM.Context{messages: [message]}

      mock_request = %Req.Request{
        options: [
          context: context,
          id: "gemini-1.5-flash",
          stream: false
        ]
      }

      updated_request = Google.encode_body(mock_request)
      decoded = Jason.decode!(updated_request.body)

      [user_msg] = decoded["contents"]
      [part] = user_msg["parts"]

      assert part["fileData"]["mimeType"] == "image/jpeg"
      # URL should be preserved with query params
      assert part["fileData"]["fileUri"] == url
    end

    test "encode_body handles data URI format in image_url" do
      base64_data = Base.encode64("fake image data")
      url = "data:image/png;base64,#{base64_data}"

      image_url_part = ReqLLM.Message.ContentPart.image_url(url)

      message = %ReqLLM.Message{
        role: :user,
        content: [image_url_part]
      }

      context = %ReqLLM.Context{messages: [message]}

      mock_request = %Req.Request{
        options: [
          context: context,
          id: "gemini-1.5-flash",
          stream: false
        ]
      }

      updated_request = Google.encode_body(mock_request)
      decoded = Jason.decode!(updated_request.body)

      [user_msg] = decoded["contents"]
      [part] = user_msg["parts"]

      # Data URIs should use inline_data format, not fileData
      assert Map.has_key?(part, "inline_data")
      assert part["inline_data"]["mime_type"] == "image/png"
      assert part["inline_data"]["data"] == base64_data
    end

    test "encode_body returns error text for unsupported URL schemes" do
      url = "ftp://example.com/file.jpg"

      image_url_part = ReqLLM.Message.ContentPart.image_url(url)

      message = %ReqLLM.Message{
        role: :user,
        content: [image_url_part]
      }

      context = %ReqLLM.Context{messages: [message]}

      mock_request = %Req.Request{
        options: [
          context: context,
          id: "gemini-1.5-flash",
          stream: false
        ]
      }

      updated_request = Google.encode_body(mock_request)
      decoded = Jason.decode!(updated_request.body)

      [user_msg] = decoded["contents"]
      [part] = user_msg["parts"]

      # Should fall back to text with error message
      assert Map.has_key?(part, "text")
      assert part["text"] =~ "Unsupported URL scheme"
    end

    test "encode_body handles malformed data URI gracefully" do
      # Data URI without the comma separator
      url = "data:image/png;base64"

      image_url_part = ReqLLM.Message.ContentPart.image_url(url)

      message = %ReqLLM.Message{
        role: :user,
        content: [image_url_part]
      }

      context = %ReqLLM.Context{messages: [message]}

      mock_request = %Req.Request{
        options: [
          context: context,
          id: "gemini-1.5-flash",
          stream: false
        ]
      }

      updated_request = Google.encode_body(mock_request)
      decoded = Jason.decode!(updated_request.body)

      [user_msg] = decoded["contents"]
      [part] = user_msg["parts"]

      assert Map.has_key?(part, "text")
      assert part["text"] =~ "Malformed data URI"
    end
  end

  describe "thought signature support" do
    test "decode_response extracts reasoning_details from thought parts" do
      google_response = %{
        "candidates" => [
          %{
            "content" => %{
              "parts" => [
                %{
                  "text" => "Let me think about this...",
                  "thought" => true,
                  "thoughtSignature" => "base64signature123"
                },
                %{"text" => "The answer is 42."}
              ],
              "role" => "model"
            },
            "finishReason" => "STOP"
          }
        ],
        "usageMetadata" => %{
          "promptTokenCount" => 10,
          "candidatesTokenCount" => 15,
          "totalTokenCount" => 25,
          "thoughtsTokenCount" => 5
        }
      }

      mock_resp = %Req.Response{
        status: 200,
        body: google_response
      }

      {:ok, model} = ReqLLM.model("google:gemini-2.5-flash")
      context = context_fixture()

      mock_req = %Req.Request{
        options: [context: context, stream: false, model: model.model]
      }

      {_req, resp} = Google.decode_response({mock_req, mock_resp})

      assert %ReqLLM.Response{} = resp.body
      response = resp.body

      assert response.message.reasoning_details != nil
      assert length(response.message.reasoning_details) == 1

      [detail] = response.message.reasoning_details
      assert %ReqLLM.Message.ReasoningDetails{} = detail
      assert detail.text == "Let me think about this..."
      assert detail.signature == "base64signature123"
      assert detail.encrypted? == true
      assert detail.provider == :google
      assert detail.format == "google-gemini-v1"
      assert detail.index == 0
      assert detail.provider_data == %{"thought" => true}
    end

    test "decode_response returns nil reasoning_details when no thought parts" do
      google_response = %{
        "candidates" => [
          %{
            "content" => %{
              "parts" => [%{"text" => "The answer is 42."}],
              "role" => "model"
            },
            "finishReason" => "STOP"
          }
        ],
        "usageMetadata" => %{
          "promptTokenCount" => 10,
          "candidatesTokenCount" => 15,
          "totalTokenCount" => 25
        }
      }

      mock_resp = %Req.Response{
        status: 200,
        body: google_response
      }

      {:ok, model} = ReqLLM.model("google:gemini-2.5-flash")
      context = context_fixture()

      mock_req = %Req.Request{
        options: [context: context, stream: false, model: model.model]
      }

      {_req, resp} = Google.decode_response({mock_req, mock_resp})

      assert %ReqLLM.Response{} = resp.body
      assert resp.body.message.reasoning_details == nil
    end

    test "encode_body includes thought parts from assistant message reasoning_details" do
      {:ok, model} = ReqLLM.model("google:gemini-2.5-flash")

      reasoning_detail = %ReqLLM.Message.ReasoningDetails{
        text: "Let me think step by step...",
        signature: "signatureABC123",
        encrypted?: true,
        provider: :google,
        format: "google-gemini-v1",
        index: 0,
        provider_data: %{"thought" => true}
      }

      assistant_message = %ReqLLM.Message{
        role: :assistant,
        content: [%ReqLLM.Message.ContentPart{type: :text, text: "The answer is 42."}],
        reasoning_details: [reasoning_detail]
      }

      user_message = %ReqLLM.Message{
        role: :user,
        content: [%ReqLLM.Message.ContentPart{type: :text, text: "What is the answer?"}]
      }

      context = %ReqLLM.Context{messages: [user_message, assistant_message]}

      mock_request = %Req.Request{
        options: [
          context: context,
          model: model.model,
          stream: false
        ]
      }

      updated_request = Google.encode_body(mock_request)
      decoded = Jason.decode!(updated_request.body)

      assert is_list(decoded["contents"])
      assert length(decoded["contents"]) == 2

      [_user_msg, model_msg] = decoded["contents"]
      assert model_msg["role"] == "model"

      parts = model_msg["parts"]
      assert length(parts) == 2

      [thought_part, text_part] = parts
      assert thought_part["thought"] == true
      assert thought_part["text"] == "Let me think step by step..."
      assert thought_part["thoughtSignature"] == "signatureABC123"

      assert text_part["text"] == "The answer is 42."
      refute Map.has_key?(text_part, "thought")
    end

    test "encode_body skips non-Google reasoning_details with warning" do
      import ExUnit.CaptureLog

      {:ok, model} = ReqLLM.model("google:gemini-2.5-flash")

      anthropic_detail = %ReqLLM.Message.ReasoningDetails{
        text: "Anthropic thinking...",
        signature: "anthropic-sig",
        encrypted?: false,
        provider: :anthropic,
        format: "anthropic-thinking-v1",
        index: 0,
        provider_data: %{"type" => "thinking"}
      }

      assistant_message = %ReqLLM.Message{
        role: :assistant,
        content: [%ReqLLM.Message.ContentPart{type: :text, text: "Response"}],
        reasoning_details: [anthropic_detail]
      }

      context = %ReqLLM.Context{messages: [assistant_message]}

      mock_request = %Req.Request{
        options: [
          context: context,
          model: model.model,
          stream: false
        ]
      }

      log =
        capture_log(fn ->
          updated_request = Google.encode_body(mock_request)
          decoded = Jason.decode!(updated_request.body)

          [model_msg] = decoded["contents"]
          parts = model_msg["parts"]

          assert length(parts) == 1
          [text_part] = parts
          assert text_part["text"] == "Response"
          refute Map.has_key?(text_part, "thought")
        end)

      assert log =~ "Skipping non-Google reasoning detail"
    end
  end

  describe "google_auth_header option for streaming" do
    setup do
      {:ok, model} = ReqLLM.model("google:gemini-2.0-flash-exp")
      context = Context.new([Context.user("test")])
      {:ok, model: model, context: context}
    end

    test "default streaming uses query param auth", %{model: model, context: context} do
      opts = [api_key: "test-api-key"]

      {:ok, finch_request} = Google.attach_stream(model, context, opts, nil)

      assert finch_request.query =~ "key=test-api-key"
      assert finch_request.query =~ "alt=sse"
      refute has_header?(finch_request.headers, "x-goog-api-key")
    end

    test "google_auth_header: true uses header auth instead of query param", %{
      model: model,
      context: context
    } do
      opts = [
        api_key: "test-api-key",
        provider_options: [google_auth_header: true]
      ]

      {:ok, finch_request} = Google.attach_stream(model, context, opts, nil)

      refute finch_request.query =~ "key="
      assert finch_request.query == "alt=sse"
      assert has_header_value?(finch_request.headers, "x-goog-api-key", "test-api-key")
    end

    test "google_auth_header: false behaves same as default", %{model: model, context: context} do
      opts = [
        api_key: "test-api-key",
        provider_options: [google_auth_header: false]
      ]

      {:ok, finch_request} = Google.attach_stream(model, context, opts, nil)

      assert finch_request.query =~ "key=test-api-key"
      refute has_header?(finch_request.headers, "x-goog-api-key")
    end

    test "google_auth_header works with custom base_url for proxies", %{
      model: model,
      context: context
    } do
      opts = [
        api_key: "proxy-api-key",
        base_url: "https://openai-proxy.example.com/v1",
        provider_options: [google_auth_header: true]
      ]

      {:ok, finch_request} = Google.attach_stream(model, context, opts, nil)

      assert finch_request.host == "openai-proxy.example.com"
      refute finch_request.query =~ "key="
      assert has_header_value?(finch_request.headers, "x-goog-api-key", "proxy-api-key")
    end

    defp has_header?(headers, name) do
      Enum.any?(headers, fn {n, _v} -> n == name end)
    end

    defp has_header_value?(headers, name, value) do
      Enum.any?(headers, fn {n, v} -> n == name and v == value end)
    end
  end

  describe "consecutive role merging for parallel tool results" do
    test "encode_body merges multiple tool results into a single user entry" do
      {:ok, model} = ReqLLM.model("google:gemini-1.5-flash")

      context =
        Context.new([
          Context.user("What's the weather in SF and NYC?"),
          Context.assistant("",
            tool_calls: [
              %ReqLLM.ToolCall{
                id: "call_1",
                type: "function",
                function: %{name: "get_weather", arguments: ~s({"location":"SF"})}
              },
              %ReqLLM.ToolCall{
                id: "call_2",
                type: "function",
                function: %{name: "get_weather", arguments: ~s({"location":"NYC"})}
              }
            ]
          ),
          Context.tool_result("call_1", "get_weather", "72F sunny"),
          Context.tool_result("call_2", "get_weather", "58F cloudy")
        ])

      mock_request = %Req.Request{
        options: [
          context: context,
          model: model.model,
          stream: false,
          operation: :chat
        ]
      }

      updated_request = Google.encode_body(mock_request)
      decoded = Jason.decode!(updated_request.body)
      contents = decoded["contents"]

      tool_result_entries =
        Enum.filter(contents, fn entry ->
          Enum.any?(entry["parts"], &Map.has_key?(&1, "functionResponse"))
        end)

      assert length(tool_result_entries) == 1,
             "Expected tool results merged into 1 entry, got #{length(tool_result_entries)}"

      [merged] = tool_result_entries
      fn_parts = Enum.filter(merged["parts"], &Map.has_key?(&1, "functionResponse"))
      assert length(fn_parts) == 2

      names = Enum.map(fn_parts, & &1["functionResponse"]["name"])
      assert "get_weather" in names
    end

    test "encode_body preserves non-consecutive same-role entries separately" do
      {:ok, model} = ReqLLM.model("google:gemini-1.5-flash")

      context =
        Context.new([
          Context.user("Hello"),
          Context.assistant("Hi there!"),
          Context.user("How are you?")
        ])

      mock_request = %Req.Request{
        options: [
          context: context,
          model: model.model,
          stream: false,
          operation: :chat
        ]
      }

      updated_request = Google.encode_body(mock_request)
      decoded = Jason.decode!(updated_request.body)
      contents = decoded["contents"]

      user_entries = Enum.filter(contents, &(&1["role"] == "user"))
      assert length(user_entries) == 2
    end
  end
end
